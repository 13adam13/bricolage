<%perl>;
my $i;
$m->comp('/widgets/wrappers/sharky/header.mc',
         title   => $lang->maketext("Select Categories"),
         context => $lang->maketext("Workflow | Profile | $disp | Select Alias"),
         wf_id   => $wf_id);

$m->print('<form method="post" action="', $r->uri,
          qq{" name="pick_cats" onSubmit="return confirmChanges(this)">\n});
$m->comp("/widgets/wrappers/sharky/table_top.mc",
         caption => $caption,
         number  => ++$i);
# Let 'em pick a category for the story they're aliasing.
$m->print('<br />', $cat_sel, '<br />');
$m->comp('/widgets/wrappers/sharky/table_bottom.mc');

# Let 'em alias related stories.
if (@rel_story) {
    $m->comp('/widgets/listManager/listManager.mc',
             object   => 'story',
             title    => 'Related %n to Alias',
             addition => undef,
             objs     => \@rel_story,
             fields   => [qw(id name uri cover_date)],
             profile  => undef,
             select   => undef,
             number   => ++$i,
	);
}

# Let 'em alias related media.
if (@rel_media) {
    $m->comp('/widgets/listManager/listManager.mc',
             object   => 'media',
             title    => 'Related %n to Alias',
             addition => undef,
             objs     => \@rel_media,
             fields   => [qw(id name uri cover_date)],
             profile  => undef,
             select   => undef,
             number   => ++$i,
	);
}

# Send 'em on up!
$m->comp("/widgets/wrappers/sharky/table_top.mc",
         caption => "Submit",
         number  => ++$i,
         ghostly => 1);

$m->comp('/widgets/profile/create_button.html',
         widget => $widget,
         cb     => 'pick_cats_cb');
$m->print("</form>\n");
$m->comp('/widgets/wrappers/sharky/footer.mc');
</%perl>
<%once>;
my $widget = 'alias';
my %classes = ( story => get_package_name('story'),
                media => get_package_name('media'),
              );
my %dispmap = ( story => get_disp_name('story'),
                media => get_disp_name('media')
              );
my $excl_sub = sub { ! chk_authz($_[0], READ, 1) };
</%once>
<%shared>;
my $caption = $lang->maketext("Select a Category");
</%shared>
<%init>;
# Load 'em on up!
my $class_key = get_state_data($widget, 'class_key');
my $disp = $dispmap{$class_key} or redirect("/");
my $aliased_id = get_state_data($widget, 'aliased_id');
my $aliased = $classes{$class_key}->lookup({ id => $aliased_id });
my $wf_id = get_state_data($widget, 'wf_id');
my $wf = Bric::Biz::Workflow->lookup({ id => $wf_id });
my $gid = $wf->get_all_desk_grp_id;
my $site_id = $wf->get_site_id;

# Find any related assets.
my (@rel_story, @rel_media);
foreach my $rel ($aliased->get_related_objects) {
    # Keep the related ID.
    my $ref = ref $rel;
    if(chk_authz($rel, READ, 1)
       and chk_authz($ref, CREATE, 0, $site_id, $gid)) {
        if ($ref->isa('Bric::Biz::Asset::Business::Story')) {
            push @rel_story, $rel->get_id;
        } else {
            push @rel_media, $rel->get_id;
        }
    }
}

# Load up the list of categories.
my $cat_sel = $m->scomp( '/widgets/select_object/select_object.mc',
                         object     => 'category',
                         disp       => $caption,
                         name       => "category_id",
                         constrain  => { site_id => $site_id },
                         exclude    => $excl_sub,
                         req        => 1,
                         field      => 'uri',
                         sort_field => 'uri',
                         useTable   => 1
                       );
</%init>
